plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.5'
	id 'io.spring.dependency-management' version '1.1.7'

	// Code Quality
	id 'checkstyle'
	id 'jacoco'
	id 'com.diffplug.spotless' version '6.25.0'
}

group = 'com.hotspot'
version = '0.0.1-SNAPSHOT'
description = 'Hotspot Admin Domain API'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

repositories {
	mavenCentral()
}

/* =========================
   Dependencies
========================= */
dependencies {
	// Web
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-validation'

	// Infra
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'

	// Swagger
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.5.0'

	// DB
	runtimeOnly 'org.postgresql:postgresql'

	// Lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	// Test
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'

	// JWT
	implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'
}

/* =========================
   Test
========================= */
tasks.named('test') {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}

/* =========================
   Spotless
========================= */
spotless {
	java {
		target 'src/**/*.java'
		// 4칸 들여쓰기 강제
		indentWithSpaces(4)

		// 개행 & 공백 정리
		trimTrailingWhitespace()
		endWithNewline()

		// import 정렬
		importOrder(
				'java',
				'javax',
				'jakarta',
				'org',
				'com',
				''
		)
		removeUnusedImports()
	}
}

/* =========================
   Checkstyle
========================= */
checkstyle {
	toolVersion = "10.12.5"
	configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")
}

tasks.withType(Checkstyle).configureEach {
	reports {
		xml.required = false
		html.required = true
	}
}

/* =========================
   JaCoCo
========================= */
jacoco {
	toolVersion = "0.8.11"
}

def reportExcludes = [
		'**/*Application*',
		'**/config/**',
		'**/dto/**',
		'**/request/**',
		'**/response/**',
		'**/entity/**',
		'**/exception/**',
		'**/common/**',
		'**/infrastructure/**'
]

def verificationExcludes = [
		'hotspot.admin.*Application*',
		'hotspot.admin.*.config.*',
		'hotspot.admin.*.dto.*',
		'hotspot.admin.*.request.*',
		'hotspot.admin.*.response.*',
		'hotspot.admin.*.entity.*',
		'hotspot.admin.*.exception.*',
		'hotspot.admin.common.*',
		'hotspot.admin.*.infrastructure.*'
]

jacocoTestReport {
	dependsOn test
	reports {
		xml.required = true
		html.required = true
	}

	classDirectories.setFrom(
			files(classDirectories.files.collect {
				fileTree(dir: it, exclude: reportExcludes)
			})
	)
}

jacocoTestCoverageVerification {
	dependsOn test

	violationRules {
		rule {
			element = 'CLASS'

			excludes = verificationExcludes

			limit {
				counter = 'INSTRUCTION'
				value = 'COVEREDRATIO'
				minimum = 0.70
			}
		}
	}
}

/* =========================
   CI Gate
========================= */
check.dependsOn(
		spotlessCheck,
		checkstyleMain,
		jacocoTestCoverageVerification
)
